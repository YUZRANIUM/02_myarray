; INFO *****************************************************
; FileName : 02_5_Ctl.hsp
; Version  : 0.27.3
; Date     : 2023/01/21
; Author   : YUZRANIUM（ゆずらにうむ）
; Twitter  : https://twitter.com/YUZRANIUM
; GitHub   : https://github.com/YUZRANIUM/02_myarray
;***********************************************************
/* Description
多次元配列をなんやかするモジュールです。
ctlarray命令による動作制御で多次元配列の部分処理を可能にします。
************************************************************/

	#cmpopt ppout 0
	#include "02_myarray.hsp"
	#const buf_size 4096
	dim hMes, 2				; オブジェクトハンドル用
	sdim moni, buf_size		; 表示用
	dim i, 4
	/*****  表示用  *****/
	screen 0, 640, 480 : font "MyricaM M", 16 : objmode 2, 1
	pos 0, 0  :  mesbox moni, 640, 480, 12 : hMes(0) = objinfo(stat, 2)

	;===================================
	; 適当に変数の準備

	sdim sary, 1024, 10, 10
	ddim fary, 12, 8, 4
	dim  iary, 17, 7, 5, 4

	setarray sary, "XYZ", 1, 1
	setarray fary, 33.0, 1, -1.25
	setarray iary, 0, 1, 1

	;===================================
	; 以下実践

	; 配列の先頭からオフセット値25番目から50番目までを表示
	ctlarray 25, 50
	priarray moni, sary

	; 配列の各次元要素数で動作範囲を指定することも可能
	ctlarray dimlinec(sary, 7, 6), dimlinec(sary)    ; 次元の指定を省略すると最後までになる

	mesans moni, dimlinec(sary, 7, 6)
	moni += "\n"

	priarray moni, sary

	mesans moni, dimlinec(sary)
	moni += "\n"


	; start - end 間は複数処理をまとめて制御することができる
	;
	ctlarray_start      // 制御フィールド 開始 //
	;=======================================================
	ctlarray dimlinec(iary, 4, 3), dimlinec(iary, 1, 0, 1, 0)    ; startの直後にctlarray命令で動作範囲を指定する必要がある
	moni += "\n\t//// 制御フィールド 開始 ////\n\n"

	priarray moni, iary, "比較用"
	setarray iary, 0, 1, -3
	priarray moni, iary, "配列の途中から代入"

	cnvarray test_ary1, iary, 3, dimlinec(iary, 1, 0, 1) + 1 - dimlinec(iary, 4, 3)

	; 制御の除外
	ctlexcld : priarray moni, test_ary1, "配列の一部分型変換コピー\niary(4, 3, 0, 0) から iary(1, 0, 1, 0)の間"


	;=======================================================
	; 新たに制御範囲を設定し直す. 以降,新たなctlarrayかctlarray_endまで適用
	;
	ctlarray dimlinec(iary, 4, 3) - 3, dimlinec(iary, 1, 0, 1) + 3
	priarray moni, iary, "制御フィールド内で制御範囲を再設定\n上下方向ともに3つずつ範囲を拡張"


	;=======================================================
	; 再再設定
	;
	ctlarray dimlinec(iary, 4, 3), dimlinec(iary, 1, 0, 1, 0)

	priarray moni, sary, "配列の長さを超えて終了値が設定された場合は\n配列の終端で動作を停止する"

	linedim fary, i, dimlinec(iary, 4, 3)
	ctlexcld : priarray moni, i, "linedim命令は配列のオフセット値から\n配列の要素数を読み出すものです"

	priarray moni, fary

	linedim fary, i, dimlinec(iary, 1, 0, 1, 0)
	ctlexcld : priarray moni, i, "終了値からfaryの各次元要素数を算出"


	;=======================================================
	ctlarray_end       // 制御フィールド 終了 //

	objprh hMes(0), moni
	stop